PROGRAM -> start BLOCK end
BLOCK -> { STMT_LIST }
STMT_LIST -> STMT STMT_LIST | ε
STMT -> DECL | ASSIGN_STMT | READ_STMT | WRITE_STMT | IF_STMT | WHILE_STMT | FOR_STMT
DECL -> TYPE ID DECL_INIT ;
DECL_INIT -> = EXPR | ε
TYPE -> int | double | siruri
ASSIGN_STMT -> ID = EXPR ;
READ_STMT -> citire ( ID ) ;
WRITE_STMT -> afisare ( EXPR ) ;
IF_STMT -> daca ( COND ) BLOCK ELSE_IF_PART ELSE_PART
ELSE_IF_PART -> altfel_daca ( COND ) BLOCK ELSE_IF_PART | ε
ELSE_PART -> altfel BLOCK | ε
WHILE_STMT -> atata_timp_cat ( COND ) BLOCK
FOR_STMT -> continua_pentru ( ASSIGN_HEADER COND ; ASSIGN_HEADER ) BLOCK
ASSIGN_HEADER -> ID = EXPR
COND -> EXPR REL_OP EXPR | EXPR
REL_OP -> == | != | < | > | <= | >=
EXPR -> TERM EXPR_PRIME
EXPR_PRIME -> ADD_OP TERM EXPR_PRIME | ε
TERM -> FACTOR TERM_PRIME
TERM_PRIME -> MUL_OP FACTOR TERM_PRIME | ε
FACTOR -> ID | CONST_NUM | CONST_SIR | ( EXPR )
ADD_OP -> + | -
MUL_OP -> * | / | %
